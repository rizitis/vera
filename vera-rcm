#! /usr/bin/perl
#
# Modify /etc/rc.d/rc.M and /etc/rc.d/rc.inet2 on the fly.
#
# Replace all instances of "/etc/rc.d/rc.SOMETHING start" with
# "vlad start system/rc.M/rc.SOMETHING", if this unit file exists.

use strict;
use warnings;

use IO::File;

my ($unitdir, $rc_m) = @ARGV;

my $fh = IO::File->new($rc_m, "r");

my $l;

sub repl {
    my ($pfix, $word,$start) = @_;

    my $unit = $word;

    # ip_forward => ip-forward
    $unit =~ s/_/-/g;

    if ($unit =~ m{^/etc/rc.d/(.*)} &&
	-f "$unitdir/system/rc.M/$1")
    {
	return "vlad start system/rc.M/$1";
    }
    return ($pfix // '') . "$word$start";
}

while (defined($l = <$fh>))
{
    # /etc/rc.d/rc.inet2 in /etc/rc.d/rc.M will run me again.

    $l =~ s{
    ^\s*/etc/rc\.d/rc\.inet2[ \t]*$
    }[
	"$0 $unitdir /etc/rc.d/rc.inet2 | /bin/bash"
    ]gex;

    # And check if any other script has a unit.
    $l =~ s{
       (\b(sh|\.)\s+)?(/etc/rc\.d/rc\.\w+)(\s+start)
       }[
	repl($1,$3,$4)
       ]gex;
    print $l;
}
